---
layout:     post
title:      FFmpeg基本命令
subtitle:   windows下FFmpeg命令使用
date:       2020-01-22
author:     yym439
header-img: img/post-bg-github-cup.jpg
catalog: true
tags:
    - FFmpeg
---

### FFmpeg命令行模式实现音视频操作
```
rem ffmpeg-bat.bat

echo off
rem 设置UTF8编码
chcp 65001 
setlocal enabledelayedexpansion

echo "1、FFmpeng剪切视频，默认去掉片头25秒"
echo "2、FFmpeng视频提取图片"
echo "3、FFmpeng图片转视频"
echo "4、FFmpeng直播推流"
echo "5、FFmpeng直播拉流"
echo.

rem 获取当前脚本路径
set DIR=%cd%

:opt
set/p option="请输入需要的功能选项："

if %option%==1 (
	IF NOT EXIST "new" (
		md  "new"
	)
	rem (参数 /R 表示需要遍历子文件夹,去掉表示不遍历子文件夹)
	rem (%%f 是一个变量,类似于迭代器,但是这个变量只能由一个字母组成,前面带上%%)
	rem (括号中是通配符,可以指定后缀名,*.*表示所有文件)
	for %%f in (*.mp3) do ( 
		ffmpeg -i "%%f"  -acodec copy -vcodec copy  -ss 00:00:25 "%DIR%\new\%%f"
		rem (del "%%f")
	)
) else if  %option%==2 (
	set /p vname="请输入视频文件路径："
	rem (-r 1：帧率，表示每秒提取一张图片;!!启用变量延迟)
	ffmpeg -i !vname! -r 0.1 -f image2 image-%%3d.jpeg
) else if  %option%==3 (
	rem 图片命名有规律,指定-r越大播放越慢
	ffmpeg -i image-%%3d.jpeg -r 5000 out.mp4
	rem -loop 1开启循环，每秒一帧，持续9秒
	rem ffmpeg -r 1 -f image2 -loop 1 -i image-%3d.jpeg -t 9 out.mp4
) else if  %option%==4 (
	set /p url="请输入推流服务器地址："
	ffmpeg -re -i out.mp4 -c copy -f flv !url!
) else if  %option%==5 (
	set /p url="请输入拉流服务器地址："
	ffmpeg -i !url! -c copy dump.m3u8
) else (
	echo "暂不支持此功能..."
	goto opt
)

echo on
pause
```